/*
 * Copyright 2017-2023 O2 Czech Republic, a.s.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'cz.o2.proxima.java-conventions'
  alias libs.plugins.protobuf
}

def scala_version = "2.12"
def jackson_version = "2.14.1"

dependencies {
  api project(':proxima-beam-core')
  api project(':proxima-tools')
  api 'org.apache.beam:beam-sdks-java-core:2.46.0'
  api 'org.apache.beam:beam-sdks-java-extensions-join-library:2.46.0'
  api 'org.apache.beam:beam-sdks-java-extensions-kryo:2.46.0'
  intoShadow libraries.grpc_core
  intoShadow libraries.grpc_protobuf
  intoShadow libraries.grpc_stub
  intoShadow libraries.grpc_netty_shaded
  api 'org.slf4j:slf4j-api:1.7.30'
  testImplementation project(path: ':proxima-core', configuration: 'testsJar')
  testImplementation project(path: ':proxima-direct-core', configuration: 'testsJar')
  testImplementation project(path: ':proxima-tools', configuration: 'testsJar')
  testImplementation 'org.apache.spark:spark-core_2.12:3.1.2'
  testImplementation 'org.slf4j:log4j-over-slf4j:1.7.33'
  testImplementation 'org.apache.spark:spark-streaming_2.12:3.1.2'
  testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.hamcrest:hamcrest-all:1.3'
  testImplementation('org.apache.beam:beam-runners-flink-1.15:2.46.0') {
    exclude group: "org.apache.flink", module: "flink-clients_2.11"
    exclude group: "org.apache.flink", module: "flink-runtime_2.11"
    exclude group: "org.apache.flink", module: "flink-streaming-java_2.11"
    exclude group: "org.apache.flink", module: "flink-optimizer_2.11"
  }
  testImplementation "org.apache.flink:flink-clients:1.15.3"
  testImplementation 'org.apache.beam:beam-runners-spark-3:2.46.0'
  testImplementation("org.apache.spark:spark-core_${scala_version}:3.1.2") {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
  }
  testImplementation 'org.apache.beam:beam-runners-direct-java:2.46.0'
  compileOnly project(':proxima-direct-core')
  compileOnly 'org.apache.beam:beam-runners-direct-java:2.46.0'
  compileOnly libraries.guava
  compileOnly 'org.apache.beam:beam-runners-flink-1.13:2.46.0'
  compileOnly 'org.apache.beam:beam-runners-spark-3:2.46.0'
  compileOnly 'org.apache.flink:flink-clients_2.12:1.13.5'
  compileOnly libraries.auto_service
  compileOnly libraries.lombok
  annotationProcessor libraries.lombok
  annotationProcessor libraries.auto_service
  testCompileOnly libraries.auto_service
  testCompileOnly libraries.lombok
  testAnnotationProcessor libraries.lombok
  testAnnotationProcessor libraries.auto_service
}

configurations.all {
  resolutionStrategy {
    //failOnVersionConflict()
    force "com.google.guava:guava:31.1-jre"
    force "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    force "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    force "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    force "com.fasterxml.jackson.module:jackson-module-paranamer:${jackson_version}"
    force "com.fasterxml.jackson.module:jackson-module-scala_${scala_version}:${jackson_version}"
  }
}

description = 'cz.o2.proxima:proxima-beam-tools'

protobuf {
  protoc { artifact = libraries.protoc }
  plugins {
    grpc { artifact = libraries.gen_grpc }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

shadowJar {
  transform(renamingTransformer()) {
    renameClosure = {
      if (it.startsWith("META-INF/native/libio_grpc_netty_shaded_")) {
        return "META-INF/native/libcz_o2_proxima_beam_tools_shaded_${it.substring(19)}"
      }
      if (it.startsWith("META-INF/native/io_grpc_netty_shaded_")) {
        return "META-INF/native/cz_o2_proxima_beam_tools_shaded_${it.substring(16)}"
      }
    }
  }
  relocate "io.grpc", "cz.o2.proxima.beam.tools.shaded.io.grpc"
}
