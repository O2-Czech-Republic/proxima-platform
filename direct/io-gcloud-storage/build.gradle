/*
 * Copyright 2017-2024 O2 Czech Republic, a.s.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'cz.o2.proxima.java-conventions'
}

dependencies {
  api project(path: ':proxima-core')
  api project(path: ':proxima-direct-core')
  api project(path: ':proxima-direct-io-blob')
  implementation "cz.o2.proxima:proxima-vendor:${project.version}"
  intoShadow 'com.google.cloud:google-cloud-storage:1.101.0'
  implementation libraries.slf4j_api
  testImplementation project(path: ':proxima-core', configuration: 'testsJar')
  testImplementation project(path: ':proxima-direct-core', configuration: 'testsJar')
  testImplementation libraries.junit4
  testImplementation libraries.mockito_core
  testImplementation libraries.slf4j_log4j
  compileOnly libraries.auto_service_annotations
  compileAnnotationProcessor libraries.lombok
  annotationProcessor libraries.auto_service
}

description = 'cz.o2.proxima:proxima-direct-io-gcloud-storage'

enableShadowJar(project) {
  if (!it.endsWith(".class")) {
    return true
  }
  def allowed = ["cz/o2/proxima", "com/sun", "META-INF"]
  if (allowed.find { s -> it.startsWith(s) } != null) {
    return true
  }
  def regexes = [~"^[^/]+.class\$"]
  regexes.find { p -> it =~ p }
}

shadowJar {
  ["io.netty", "io.grpc", "io.opencensus", "com.google", "org.codehaus.jackson",
   "com.fasterxml", "org.apache.commons", "org.codehaus", "org.apache", "org.threeten",
   "org.conscrypt", "io.perfmark"].each {

    relocate it, "cz.o2.proxima.gcloud.storage.shaded.${it}"
  }
  exclude "javax/annotation/**"
}

publishArtifacts(project, "default")
