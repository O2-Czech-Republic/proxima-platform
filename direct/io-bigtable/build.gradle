/*
 * Copyright 2017-2023 O2 Czech Republic, a.s.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'cz.o2.proxima.java-conventions'
}

dependencies {
  api project(path: ':proxima-core')
  api project(path: ':proxima-direct-core')
  implementation libraries.slf4j_api
  intoShadow(project(path: ':proxima-direct-io-hbase-bindings')) {
    exclude group: "cz.o2.proxima", module: "proxima-core"
    exclude group: "cz.o2.proxima", module: "proxima-direct-core"
  }
  intoShadow('com.google.cloud.bigtable:bigtable-hbase-2.x:1.12.0') {
    exclude group: "org.slf4j", module: "slf4j-api"
  }
  intoShadow('com.google.cloud.bigtable:bigtable-client-core:1.12.0') {
    exclude group: "org.slf4j", module: "slf4j-api"
    exclude group:"junit", module: "junit"
  }
  intoShadow 'xerces:xercesImpl:2.12.2'
  testImplementation libraries.junit4
  testImplementation libraries.slf4j_log4j
  testImplementation libraries.mockito_core
  testRuntimeOnly libraries.log4j_over_slf4j
  compileOnly libraries.auto_service_annotations
  annotationProcessor libraries.auto_service
}

description = 'cz.o2.proxima:proxima-direct-io-bigtable'

enableShadowJar(project) {
  if (!it.endsWith(".class")) {
    return true
  }
  def allowed = ["cz/o2/proxima", "com/sun", "org/xerial", "jline", "tables/", "license/", "META-INF"]
  if (allowed.find { s -> it.startsWith(s) } != null) {
    return true
  }
  def regexes = [~"^[^/]+.class\$"]
  regexes.find { p -> it =~ p }
}

shadowJar {
  transform(renamingTransformer()) {
    renameClosure = {
      if (it.startsWith("META-INF/native/libio_grpc_netty_shaded_")) {
        return "META-INF/native/libcz_o2_proxima_bigtable_shaded_${it.substring(19)}"
      }
      if (it.startsWith("META-INF/native/io_grpc_netty_shaded_")) {
        return "META-INF/native/cz_o2_proxima_bigtable_shaded_${it.substring(16)}"
      }
    }
  }

  exclude "org/junit/**"
  exclude "junit/**"
  exclude "org/hamcrest/**"
  exclude "javax/annotation/**"
  exclude "org/slf4j/**"
  exclude "android/annotation/**"

  ["io.grpc", "io.opencensus", "com.google", "com.fasterxml", "org.apache",
   "javax.xml", "javax.servlet", "javax.ws", "javax.activation", "javax.el",
   "org.jboss", "org.codehaus", "org.objectweb", "org.eclipse", "org.threeten",
   "net.minidev", "com.thoughtworks", "com.jcraft", "org.w3c", "com.nimbusds",
   "net.jcip", "edu.umd", "org.xbill", "com.ctc", "org.xml", "jersey.repackaged",
   "com.codahale"].each {

    relocate it, "cz.o2.proxima.bigtable.shaded.${it}"
  }

  exclude "org/slf4j/*"
}

publishArtifacts(project, "default")
