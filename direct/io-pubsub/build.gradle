/*
 * Copyright 2017-2023 O2 Czech Republic, a.s.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'cz.o2.proxima.java-conventions'
}

dependencies {
  api project(':proxima-direct-core')
  implementation libraries.slf4j_api
  intoShadow project(':proxima-io-pubsub')
  intoShadow 'com.google.cloud:google-cloud-pubsub:1.101.0'
  intoShadow 'com.google.protobuf:protobuf-java:3.21.12'
  testImplementation project(path: ':proxima-core', configuration: 'testsJar')
  testImplementation libraries.slf4j_log4j
  testImplementation libraries.junit4
  testImplementation libraries.mockito_core
  compileOnly libraries.auto_service_annotations
  compileAnnotationProcessor libraries.lombok
  annotationProcessor libraries.auto_service
}

description = 'cz.o2.proxima:proxima-direct-io-pubsub'

shadowJar {
  relocators.clear()
  transform(renamingTransformer()) {
    renameClosure = {
      if (it.startsWith("META-INF/native/libio_grpc_netty_shaded_")) {
        return "META-INF/native/libcz_o2_proxima_pubsub_shaded_${it.substring(19)}"
      }
      if (it.startsWith("META-INF/native/io_grpc_netty_shaded_")) {
        return "META-INF/native/cz_o2_proxima_pubsub_shaded_${it.substring(16)}"
      }
    }
  }

  ["io.grpc", "io.opencensus", "com.google.api", "com.google.pubsub", "com.google.cloud",
   "com.google.common", "com.google.auth", "com.google.iam", "com.google.protobuf",
    "com.google.gson", "com.fasterxml", "org.apache.commons", "org.codehaus"].each {

    relocate it, "cz.o2.proxima.pubsub.shaded.${it}"
  }
}

registerTestsJar(project)
